alias fcode R1;
alias pid R2;
if(fcode==1)then        //Get Free Process Table Entry
    alias i R2;
    i=0;
    while(i<16)do
        if([PROCESS_TABLE +16*i+4]==TERMINATED)then
            [PROCESS_TABLE +16*i+ 1] = i;
            [PROCESS_TABLE +16*i+4]=ALLOCATED;
            [PROCESS_TABLE +16*i+ 14] = PAGE_TABLE_BASE+20*i;
            [PROCESS_TABLE +16*i+ 15] = 10;
            R0=i;
            return;
        endif;
        i=i+1;
    endwhile;
    R0=-1;
else if(fcode==2)then   //Free User Area Page
    alias i R3;
    i=[PROCESS_TABLE + pid * 16 + 11]*512+RESOURCE_TABLE_OFFSET;
    R4=i+16;
    while(i<R4)do
        if([i]==SEMAPHORE)then
            multipush(R1,R2,R3,R4);
            R1=7;
            R2=[i+1];
            R3=[SYSTEM_STATUS_TABLE+1];
            call RESOURCE_MANAGER;
            multipop(R1,R2,R3,R4);
        else if([i]==FILE)then
            multipush(R1,R2,R3,R4);
            R1=4;
            R2=[i+1];
            call FILE_MANAGER;
            multipop(R1,R2,R3,R4);
            endif;
        endif;
        i=i+2;
    endwhile;
    multipush(R1,R2);
    R1=2;
    R2=[PROCESS_TABLE + pid * 16 + 11];
    call MEMORY_MANAGER;
    multipop(R1,R2);
else if(fcode==3)then   //Exit Process
    if([PROCESS_TABLE + pid * 16 + 9]!=9)then
        alias i R3;
        i=0;
        while(i<16)do
            if([PROCESS_TABLE +16*i+2]==[SYSTEM_STATUS_TABLE+1])then
                [PROCESS_TABLE +16*i+2]=-1;
            endif;
            if([PROCESS_TABLE +16*i+4]==WAIT_PROCESS&&[PROCESS_TABLE +16*i+5]==pid)then
                [PROCESS_TABLE +16*i+4]=READY;
                break;
            endif;
            i=i+1;
        endwhile; 
    endif;
    multipush(R1,R2);
    R1=4;
    call PROCESS_MANAGER;
    multipop(R1,R2);
    multipush(R1,R2);
    R1=2;
    call PROCESS_MANAGER;
    multipop(R1,R2);
    [PROCESS_TABLE + pid * 16 + 4]=TERMINATED;
else if(fcode==4)then   //Free Page Table
    alias pgte R3;
    pgte=PAGE_TABLE_BASE+20*pid;
    [pgte] = -1;
    [pgte+1] = "0000";
    [pgte+2] = -1;
    [pgte+3] = "0000";
    pgte=pgte+4;
    while(pgte<PAGE_TABLE_BASE+20*(pid+1))do
        if([pgte] != -1)then
            multipush(R1,R2,R3);
            R1=2;
            R2=[pgte];
            call MEMORY_MANAGER;
            multipop(R1,R2,R3);
        endif;
        [pgte] = -1;
        [pgte+1] = "0000";
        pgte=pgte+2;
    endwhile;
    alias i R4;
    i=DISK_MAP_TABLE+pid*10+2;
    while(i<DISK_MAP_TABLE+pid*10+4)do
        if([i]!=-1)then
            multipush(R1,R2,R3,R4);
            R1=4;
            R3=pid;
            R2=[i];
            call MEMORY_MANAGER;
            multipop(R1,R2,R3,R4);
        endif;
        i=i+1;
    endwhile;
    i=DISK_MAP_TABLE+pid*10+8;
    while(i<DISK_MAP_TABLE+(pid+1)*10)do
        if([i]!=-1)then
            multipush(R1,R2,R3,R4);
            R1=4;
            R3=pid;
            R2=[i];
            call MEMORY_MANAGER;
            multipop(R1,R2,R3,R4);
        endif;
        i=i+1;
    endwhile;
    i=DISK_MAP_TABLE+pid*10;
    while(i<DISK_MAP_TABLE+(pid+1)*10)do
        [i]=-1;
        i=i+1;
    endwhile;
endif;
endif;
endif;
endif;
return;